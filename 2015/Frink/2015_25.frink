// Solution for 2015 Day 25: Let It Snow
// https://adventofcode.com/2015/day/25
// Written in Frink by Chris Hansen
// Website: https://circuitsandcode.wehappyfew.net/
// Twitter: https://twitter.com/CircuitsAndCode
// Github: https://github.com/circuitsandcode
// MIT License: https://github.com/circuitsandcode/AdventOfCode/blob/master/LICENSE

start = now[]

// Import the input file using the read function. This will import the file into a single string.
// The input string is relative to my frink.jar file. Adjust this to the absolute or relative path with your input file.
InstructionsStr = read["file:../Projects/AdventOfCode/2015/InputFiles/2015_25_input.txt"]
InstructionsReg = InstructionsStr =~ %r/row\s(\d+),\scolumn\s(\d+)\.$/

Instructions = toArray[InstructionsReg]
rowEnd = parseInt[Instructions@0]
columnEnd = parseInt[Instructions@1]

/*
The codes are filled in by diagonals: starting with the first row with an empty first box,
the codes are filled in diagonally up and to the right.
For example, the 12th code would be written to row 4, column 2; the 15th code would be
written to row 1, column 5.
The first code is 20151125. After that, each code is generated by taking the previous one,
multiplying it by 252533, and then keeping the remainder from dividing that value by 33554393.

   |    1         2         3         4         5         6
---+---------+---------+---------+---------+---------+---------+
 1 | 20151125  18749137  17289845  30943339  10071777  33511524
 2 | 31916031  21629792  16929656   7726640  15514188   4041754
 3 | 16080970   8057251   1601130   7981243  11661866  16474243
 4 | 24592653  32451966  21345942   9380097  10600672  31527494
 5 |    77061  17552253  28094349   6899651   9250759  31663883
 6 | 33071741   6796745  25397450  24659492   1534922  27995004
*/

// Starting Code
code = 20151125
goalMet = false
row = 1
column = 1

while !goalMet
{
   row = row + 1
   column = 1
   rowStart = row
   while row > 0
   {
      // Each code is generated by taking the previous one, multiplying it by 252533, and then keeping the remainder from dividing that value by 33554393
      code = (code * 252533) % 33554393
      column = column + 1
      row = row - 1
      if row == rowEnd-1 && column == columnEnd+1
      {  
         goalMet = true
         break
      }
   }
   row = rowStart
}

println["Final code at row $rowEnd and column $columnEnd is $code"]

end = now[]
println["Program run time: " + ((end - start)*1.0 -> "seconds")]